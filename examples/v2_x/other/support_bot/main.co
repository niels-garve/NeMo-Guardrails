import core
import llm

flow main
    activate llm continuation
    # activate automating intent detection
    # activate generating user intent for unhandled user utterance

    ##################################################
    # Intent detection                               #
    ##################################################

    while True
        when user greeted
            reaction to user greeting
        or when user said goodbye
            handling user leaving
        or when user requested contact details update
            handling contact details update
        or when user asked about the jobs report
            handling jobs report question
        or when user engaged in chitchat
            handling chitchat
        or when unhandled user intent # For any user utterance that does not match
            bot say "My apologies, I can't answer your question"

##################################################
# Intents                                        #
##################################################

flow user greeted
    user said "Hi"
        or user said "Hello"
        or user said "Hey"
        or user said "Good morning"
        or user said "Good afternoon"
        or user said "Good evening"
        or user said "Hi there"
        or user said "Howdy"
        or user said "What's up?"
        or user said "Greetings"
        or user said "Hey there"
        or user said "Hiya"
        or user said "Yo"
        or user said "Hi, how are you?"
        or user said "Hello, how's it going?"
        or user said "What's good?"
        or user said "Hey, how have you been?"
        or user said "Hello, anyone there?"
        or user said "Hey, you around?"

flow user said goodbye
    user said "Bye"
        or user said "See you"
        or user said "Goodbye"
        or user said "Catch you later"
        or user said "Talk to you soon"
        or user said "See you later"
        or user said "I'm out"
        or user said "Peace"
        or user said "Take care"
        or user said "Farewell"
        or user said "See you next time"
        or user said "Later"
        or user said "I'm heading out"
        or user said "See you soon"
        or user said "Bye for now"
        or user said "Have a good one"
        or user said "Goodnight"
        or user said "Till next time"
        or user said "I'm signing off"

flow user requested contact details update
    user said "I want to update my phone number"
        or user said "How can I change my email address?"
        or user said "I'd like to modify my contact information"
        or user said "Can I update my contact details?"
        or user said "I need to change my address"
        or user said "How do I update my contact info?"
        or user said "I want to change my email"
        or user said "Can I update my phone number?"
        or user said "I need to change my contact details"
        or user said "How can I update my address?"
        or user said "I want to edit my contact information"
        or user said "I need to modify my phone number"
        or user said "Can I update my email address?"
        or user said "I want to change my home address"
        or user said "How do I change my contact details?"
        or user said "I need to update my phone number"
        or user said "How do I change my email?"
        or user said "I'd like to change my contact info"
        or user said "Can I change my address?"
        or user said "I want to update my details"

flow user asked about the jobs report
    user said "Did unemployment rate change in March 2023?"
        or user said "March's job numbers in leisure and hospitality?"
        or user said "Part-time job stats for March 2023?"
        or user said "How did government jobs do in March?"
        or user said "Any major health care job changes last March?"
        or user said "March unemployment rate for Hispanics?"
        or user said "Info on long-term unemployed in March?"
        or user said "Hourly earnings update for March 2023?"
        or user said "People wanting jobs but not in the labor force in March?"
        or user said "Discouraged workers in March, any news?"
        or user said "Retail trade jobs update for March?"
        or user said "Did work hours change in March?"
        or user said "Job losses in transportation for March?"
        or user said "Labor force participation rate in March?"
        or user said "Significant employment shifts in March?"
        or user said "Trends in business services jobs for March?"
        or user said "Any revisions in past job data for March?"
        or user said "Teen job situation in March?"
        or user said "Food services job update for March?"
        or user said "Construction job changes in March?"

flow user engaged in chitchat
    # TODO possibly extend with `...`?
    user said "How are you?"
        or user said "What's up?"
        or user said "How's it going?"
        or user said "Anything interesting today?"
        or user said "What are you up to?"
        or user said "How’s your day?"
        or user said "Hope you’re doing well"
        or user said "What do you think about today?"
        or user said "Do you have any plans for today?"
        or user said "It's nice to talk to you"

##################################################
# Handler                                        #
##################################################

flow reaction to user greeting
    global $name

    if $name
        bot say "Hi {$name}!"
    else
        bot say "Hi there! What's your name?"
        await user said something as $name_ref
        $name = $name_ref.transcript

        start_new_flow_instance: # Start a new instance of the flow and continue with this one

flow handling user leaving
    bot say "Goodbye, see you next time!"

flow handling contact details update
    global $is_authenticated

    if $is_authenticated
        bot say "Please provide your new contact details:"
        await user said something as $contact_ref
        bot say "Got it, thanks!"
    else
        handling authentication
        start_new_flow_instance: # Start a new instance of the flow and continue with this one

flow handling authentication
    global $name
    global $email
    global $is_authenticated

    $is_authenticated = False

    bot say "Sure thing! Please enter your email address. I'll send you a code that authenticates it's you"
    await user said something as $email_ref
    $email = $email_ref.transcript
    $token = await SendAuthenticationEmailAction(email_transcript=$email_ref.transcript)

    if $token != "None"
        bot say "Email sent successfully to {$email}. Please enter the token below:"

        $i = 3
        while $i != 0
            await user said something as $retry_ref

            if $retry_ref.transcript == $token
                $is_authenticated = True
                if $name
                    bot say "Thanks, {$name}!"
                else
                    bot say "Thanks!"
                break
            else
                bot say "The token you entered is incorrect. Please try again. You have {$i} more chances."
            $i = $i - 1
    else
        bot say "Failed to send email to {$email}. Did I spell it correctly?"

flow handling jobs report question
    """Use the following pieces of context to answer the question at the end.
    If you don't know the answer, just say that you don't know, don't try to make up an answer.
    Use three sentences maximum and keep the answer as concise as possible.
    Always start your answer with "bot say ", and enclose it in triple quotes: bot say '''[your answer]'''
    (no "." at the end)

    # Context:
    {{ relevant_chunks }}

    Question: {{ last_user_message }}

    Your Answer:"""
    $relevant_chunks = await RetrieveRelevantChunksAction()
    ...

flow handling chitchat
    llm continue interaction
